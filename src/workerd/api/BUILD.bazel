load("//:build/wd_cc_capnp_library.bzl", "wd_cc_capnp_library")
load("//:build/kj_test.bzl", "kj_test")
load("//:build/wd_test.bzl", "wd_test")
load("//:build/wd_cc_library.bzl", "wd_cc_library")

filegroup(
    name = "srcs",
    srcs = glob(
        ["**/*.c++"],
        exclude = [
            "**/*test*.c++",
            "html-rewriter.c++",
            "r2*.c++",
        ],
    ),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "hdrs",
    srcs = glob(
        ["**/*.h"],
        exclude = ["*test*.h",  "r2*.h"],
    ),
    visibility = ["//visibility:public"],
)

# As a step towards modularizing the io library, turn html-rewriter into its own library. Since
# the server implementation is the only code depending on it, we can compile it on its own, which
# allows us to not link test targets with the rust dependencies reducing test binary sizes and
# link times.
wd_cc_library(
    name = "html-rewriter",
    srcs = ["html-rewriter.c++"],
    hdrs = [":hdrs"],
    visibility = ["//visibility:public"],
    deps = [
        "//src/workerd/io",
        "@com_cloudflare_lol_html//:lolhtml",
    ],
)

wd_cc_library(
    name = "r2-api",
    srcs = ["r2-admin.c++", "r2-bucket.c++", "r2-multipart.c++", "r2-rpc.c++"],
    hdrs = ["r2-admin.h", "r2-bucket.h", "r2-multipart.h", "r2-rpc.h", "r2.h"],
    visibility = ["//visibility:public"],
    deps = [
        ":r2-api_capnp",
        "//src/workerd/io",
    ],
)

wd_cc_capnp_library(
    name = "r2-api_capnp",
    srcs = ["r2-api.capnp"],
    visibility = ["//visibility:public"],
)

wd_cc_capnp_library(
    name = "analytics-engine_capnp",
    srcs = ["analytics-engine.capnp"],
    visibility = ["//visibility:public"],
)

[kj_test(
    src = f,
    deps = [
        "//src/workerd/io",
    ],
) for f in glob(
    ["**/*-test.c++"],
    exclude = [
        "node/*-test.c++",
        "api-rtti-test.c++",
    ],
)]

kj_test(
    src = "api-rtti-test.c++",
    deps = [
        "//src/workerd/io",
        "//src/workerd/jsg:rtti",
        ":html-rewriter",
    ],
)

kj_test(
    src = "node/buffer-test.c++",
    deps = ["//src/workerd/tests:test-fixture"],
)

[wd_test(
    src = f,
    args = ["--experimental"],
    data = [f.removesuffix(".wd-test") + ".js"],
) for f in glob(
    ["**/*.wd-test"],
)]
